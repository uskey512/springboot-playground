plugins {
	id 'org.springframework.boot' version '2.5.1'
	id 'io.spring.dependency-management' version '1.0.11.RELEASE'
	id 'com.gorylenko.gradle-git-properties' version '2.3.1'
	id 'com.github.ben-manes.versions' version '0.39.0'
	id 'java'
}

group = 'com.uskey512'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '16'

repositories {
	mavenCentral()
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-web'
	implementation 'io.sentry:sentry-spring-boot-starter:5.0.1'

	testImplementation 'org.springframework.boot:spring-boot-starter-test'
}

test {
	useJUnitPlatform()
}

gitProperties {
	extProperty = 'gitProps'
}

generateGitProperties.outputs.upToDateWhen { false }

task appendReleaseInfo(dependsOn: 'generateGitProperties') {
	doLast {
		File sentryConfig = file("build/resources/main/sentry.yml")
		def release = project.ext.gitProps['git.tags'] ?: project.ext.gitProps['git.commit.id']
		sentryConfig.append("\nrelease: " + release)
	}
}

build {
	finalizedBy "appendReleaseInfo"
}

bootRun {
	doFirst {
		generateGitProperties
		def release = project.ext.gitProps['git.tags'] ?: project.ext.gitProps['git.commit.id']
		jvmArgs = [
				'-Dsentry.release=' + release
		]
	}
}
